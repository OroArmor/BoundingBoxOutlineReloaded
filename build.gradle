buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'de.undercouch:gradle-download-task:3.4.3'
    }
}

apply plugin: 'de.undercouch.download'
apply plugin: 'java'

group 'com.irtimaled'
version project.buildVersion + '-' + project.mcVersion
archivesBaseName = 'BBOutlineReloaded'

sourceCompatibility = 1.8
targetCompatibility = 1.8

compileJava {
    dependsOn 'downloadServer'
}

repositories {
    mavenCentral()
}

dependencies {
    compile files('run/craftbukkit-1.14.4.jar')
}

sourceSets {
    main
}

jar {
    destinationDir = new File('run/plugins')

    manifest {
        attributes([
                "Main-Class": "com.irtimaled.bbor.Main",
                "Specification-Title": "bbor",
                "Specification-Vendor": "Irtimaled",
                "Specification-Version": "1", // We are version 1 of ourselves
                "Implementation-Title": project.name,
                "Implementation-Version": "${version}",
                "Implementation-Vendor": "Irtimaled",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }

    classifier = 'bukkit'
}

processResources {
    filesMatching('plugin.yml') {
        expand([
                'version': project.version
        ])
    }
}

artifacts {
    archives jar
}

task downloadServer(type: Download) {
    src 'https://cdn.getbukkit.org/craftbukkit/craftbukkit-1.14.4-R0.1-SNAPSHOT.jar'
    dest new File('run/craftbukkit-1.14.4.jar')
    overwrite false
}

task runServer(dependsOn: jar, type: JavaExec) {
    workingDir = new File('run');

    main = "-jar";
    args = [
            "craftbukkit-1.14.4.jar"
    ]
}
